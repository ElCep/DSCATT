

to demographie
  ;; taux de croissance du systeme
  
  ask cuisines [
    ifelse bilan-nourriture > 0 [
      let entrants_ sum(map [ t -> ifelse-value (random-float 1) < t [1] [0] ] (n-values taille [croissance-demographique]))
      set taille taille + entrants_
      set entrants entrants + entrants_
    ] [
      let sortants_ ceiling (abs(bilan-nourriture) / (kg-nourriture-par-pers-jour * 365))
      ifelse taille - sortants_ < min-taille-cuisine and absorption [
        let cuisine-absorbante one-of other cuisines 
        if(cuisine-absorbante != NOBODY) [
        absorbe-cuisine cuisine-absorbante self
        ]
      ] [
        set taille max list (taille - sortants_) 0
        set sortants sortants + sortants_
      ]
    ]
  ]
end

to absorbe-cuisine [cuisine-absorbante cuisine-disparait]
  ask cuisine-absorbante [
    set taille taille + [taille] of cuisine-disparait
    ask patches with [ proprietaire = cuisine-disparait ] [
      set proprietaire cuisine-absorbante
      set pcolor [color] of myself 
    ]
  ]
  ask cuisine-disparait [ die ]
end


to distiribution-population-par-cuisine
 
  ask cuisines [
    set taille round random-normal moyenne-ppc sd-ppc
  ]
  
end


