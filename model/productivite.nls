;; -22 quand pas de fumier  (dire d'acteur)
;; animaux qui mange quand le champs qui sont dans la jachère font perdre de la récole

to-report production [c]  
 
  let pids sentence ([ idmyParcellesCultiveA ] of c) ([ idmyParcellesCultiveM ] of c)
  let parcelles map [pid -> patches with [ parcelle-id = pid ]] pids
 
  let prod map [ p -> production-parcelle p ] parcelles
  report sum prod
end

to-report production-parcelle[pid]
    
  if not any? pid [ report 0 ]
  
  let parcelle-fertilite sum [fertilite] of pid
  
  let raw-production 0
  let surfaceHA count pid * surface-de-patch / 10000
  
  if ([couvert-type] of (one-of pid) = "M") [set raw-production surfaceHA * kg-mil-par-ha]
  if ([couvert-type] of (one-of pid) = "A") [set raw-production surfaceHA * kg-arachide-par-ha * ratio-arachide-subsistence * taux-arachide-nourriture]
  
  let malus-f ifelse-value parcelle-fertilite <= 0 [ malus-fertilite ] [0] ;; malus fertilite
  let malus-j ifelse-value [ cycle ] of (one-of pid) = 3  [ malus-in-jachere ] [0] ;; malus jachere
  let malus malus-f + malus-j ;; malus de prod
  
  report raw-production - raw-production * malus
end


to ordre-parcelles
;; chaque cuisine doit mettre en culture ses parcelles pour nourrir ses membre.
;; il faut donc avoir calculer le besoin de chaque cuisine
;; 1 trouver les parcelles les plus proches, c.a.d. reordonnancer les id des parcelles par la distance
  
  ask cuisines [
    let myParcelles remove-duplicates [parcelle-id] of patches with [proprietaire = myself] ; liste des ID de toutes mes parcelles
    ;; pour chaque parcelle je récupère la distance minimal
    let patchDistance []
    foreach myParcelles [
      x -> let minDist min-one-of patches with[parcelle-id = x][distance myself] ;renvoie un patches qui a la plus petite distance
      ask minDist [
       set myDistFromCuisine distance myself 
      ]
    ]
    
    let mySmallPatches patches with[proprietaire = myself and myDistFromCuisine != 0]
    let myParcellesSorted sort-by [[p1 p2]-> distance p1 < distance p2 ] mySmallPatches
    set idmyParcellesSorted []
    foreach myParcellesSorted [
      x -> let _a [parcelle-id] of x
      set  idmyParcellesSorted lput _a idmyParcellesSorted
    ]
    ]
end

to planif-culture
; recupérer les surface des parcelles 
; mettre en culture
; mettre a jour mon besoin et mettre en culture temps que le les besoin ne sont pas 
  ask couverts [die]
  ask cuisines [
    let actualBesoin calcul-besoin-nourriture  [taille] of self
    let i length idmyParcellesSorted
    let iter 1
    foreach  idmyParcellesSorted [
      x -> let myactualParcelle patches with[parcelle-id = x and couvert-type != "J"]
      
      let nbPatches count myactualParcelle
      if (nbPatches > 0) [
      ifelse (actualBesoin > 0) [ ; temps que je n'ai pas assouvie mes besoin j'itère sur mes parcelles
        let surface-parcelleHA nbPatches * surface-de-patch  /  10000
        
        let besoin-assouvi-parcelle 0
        let aPatch one-of myactualParcelle
          
        if ([couvert-type] of aPatch = "M") [
            set besoin-assouvi-parcelle surface-parcelleHA * kg-mil-par-ha
            set idmyParcellesCultiveM lput x idmyParcellesCultiveM
          ]
        
        if ([couvert-type] of aPatch = "A") [
          set besoin-assouvi-parcelle surface-parcelleHA * kg-arachide-par-ha * ratio-arachide-subsistence * taux-arachide-nourriture
            set idmyParcellesCultiveA lput x idmyParcellesCultiveA
        ]
        
        set actualBesoin actualBesoin - besoin-assouvi-parcelle
        ask myactualParcelle [
          set cultived? TRUE  
        ]
      
        if (i = iter)[
          set tropParcelles? FALSE
          ;;show "pas assez de parcelle"
        ]
        set iter iter + 1
      ][
        set tropParcelles? TRUE
      ]
    ]
    ]
  ]
  
end


to update-inti-tick
  ask patches [
    set cultived? FALSE
  ]
  
  ask cuisines [
   set tropParcelles? FALSE
    set idmyParcellesCultiveM []
    set idmyParcellesCultiveA []
  ]
  
end


to mise-en-culture
  ask patches with[cultived? = TRUE][
          sprout-couverts 1 [
            set shape "plant"
          ]
  ]
end
